grammar cz.lzaruba.modelx.ModelX with org.eclipse.xtext.xbase.annotations.XbaseWithAnnotations

generate modelX "http://www.lzaruba.cz/modelx/ModelX"

Model:
	elements+=Package*;
	
ElementWithFeatures:
	Entity | Interface
;
	
AbstractElement:
	ElementWithFeatures | Enum | DataType;
	
Interface:
	(comment+=ML_COMMENT)?
	(annotations+=XAnnotation)*
	'interface' name=ValidID ('extends' supertypes+=[Interface] (',' supertypes+=[Interface])*)? '{'
		features+=Feature*
	'}'
;

Enum:
	(comment+=ML_COMMENT)?
	'enum' name=ValidID '{'
		literals+=ID*
	'}'
;

DataType:
	(comment+=ML_COMMENT)?
	'datatype' name=ValidID ':' type=JvmParameterizedTypeReference
;

Import:
    'import' importedNamespace=QualifiedNameWithWildcard;

Package:
	(comment+=ML_COMMENT)?
	'package' name=QualifiedName '{'
		imports+=Import*
		elements+=AbstractElement*
	'}';

Entity:
	(comment+=ML_COMMENT)?
	'entity' name=ValidID ('extends' superType=[Entity])? ('implements' interfaces+=[Interface] (',' interfaces+=[Interface])*)? '{'
		features+=Feature*
	'}';

Feature:
	(comment+=ML_COMMENT)?
	(many?='many')? (required?='required')? name=ValidID ':' type=[AbstractElement];
	